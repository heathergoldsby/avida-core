##############################################################################
## Copyright (C) 1999-2006 Michigan State University                        ##
## Based on work Copyright (C) 1993-2003 California Institute of Technology ##
##                                                                          ##
## Read the COPYING and README files, or contact 'avida@alife.org',         ##
## before continuing.  SOME RESTRICTIONS MAY APPLY TO USE OF THIS FILE.     ##
##############################################################################

import os
import sys
sys.path.append('support/utils')
import AvidaUtils

environment = Environment()
environment.Export('environment')

# Parse cmd line, set compiler options, generate help text.
#
# Per-platform build options are defined in
# support/utils/AvidaUtils/PlatformTool.py
#
# Command-line build options are defined in
# support/utils/AvidaUtils/CmdLineOpts.py
#
# Options for SCons are defined in
# support/utils/AvidaUtils/SConsOpts.py
#
# Static help text is defined in
# support/utils/AvidaUtils/StaticHelp.py
#

# XXX Must not use default scons signatures database; as of SCons
# 0.96.93, default database seems to be too-easily corrupted.
# @kgn 06-Dec-6
import anydbm
SConsignFile('scons-signatures', anydbm)
AvidaUtils.Configure(ARGUMENTS, environment)

if environment.subst('$enableSerialization') in ['1', 'yes']:
  environment.Append(CPPDEFINES = ['ENABLE_SERIALIZATION=1'])

if environment.subst('$enablePyPkg') in ['1', 'yes']:
  environment.Append(CPPPATH = ['#/source/bindings/Boost.Python'])

if environment.subst('$enableMemTracking') in ['1', 'yes']:
  #environment.SetDefault(enableSharedPtr = 1)
  environment.Append(CPPDEFINES = ['USE_tMemTrack=1'])

if environment['enableTCMalloc'] in ('True', '1', 1):
  environment.Append(
    CPPPATH = [ '#/source/platform/tcmalloc', ],
    LIBPATH = [ '#$buildDir/platform/tcmalloc', ],
  )

environment.Append(
  CPPPATH = [
    '#/source',
    '#/source/actions',
    '#/source/analyze',
    '#/source/archive',
    '#/source/classification',
    '#/source/cpu',
    '#/source/drivers',
    '#/source/event',
    '#/source/main',
    #'#/source/third-party/boost',
    '#/source/tools',
  ],
  LIBPATH = [
    '#$buildDir/actions',
    '#$buildDir/analyze',
    #'#$buildDir/archive',
    '#$buildDir/classification',
    '#$buildDir/cpu',
    '#$buildDir/drivers',
    #'#$buildDir/event',
    '#$buildDir/main',
    #'#$buildDir/third-party/boost/serialization',
    '#$buildDir/tools',
  ],
)

# Tell SCons where to find its subscripts.
environment.SConscript('source/SConscript',
  build_dir = '$buildDir',
  duplicate = 0,
)
environment.SConscript('support/config/SConscript')

# XXX beginnings of consistency tests. @kgn
environment.SConscript('consistencytests/SConscript',
  build_dir = 'consistencytest_output',
  duplicate = 0,
)

# XXX beginnings of 'extras'. @kgn
if environment.subst('$extrasDir') not in ['None', 'none', '']:
  environment.SConscript(
    os.path.join(environment.subst('$extrasDir'), 'SConscript'),
    build_dir = '$extrasBuildDir',
    duplicate = 0
  )


if environment['PLATFORM'] == 'win32':
  environment.Import('avida_exe')
  script_to_build_avida = environment.File(
    os.path.join('#', os.path.basename(sys.argv[0]))
  ).abspath
  environment.MSVSProject(
    target = 'Avida' + environment['MSVSPROJECTSUFFIX'],
    srcs = environment['avida_msvs_project_srcs'],
    incs = environment['avida_msvs_project_incs'],
    misc = environment['avida_msvs_project_misc'],
    variant = 'Release',
    buildtarget = avida_exe,
    #runfile = avida[0].abspath,
    MSVSSCONS = '"%s" "%s"' % (sys.executable, script_to_build_avida),
    MSVSSCONSFLAGS = '-C "${MSVSSCONSCRIPT.dir.abspath}" -f "${MSVSSCONSCRIPT.name}"'
  )

# Vim modeline to tell Vim that this is a Python script.
# vim: set ft=python:
